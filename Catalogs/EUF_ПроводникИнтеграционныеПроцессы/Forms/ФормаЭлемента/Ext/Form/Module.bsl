
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.EUF_ПроводникИнтеграционныеПроцессы.ИдентификаторПроцессаУникален(ТекущийОбъект, Отказ);
	Если Отказ Тогда
		Возврат;			
	КонецЕсли;	
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ПроверкаВыполнена", Истина);	
	
КонецПроцедуры
 
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторПроцессаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	ТекстВопроса = "Будет сгенерирован идентификатор в транслите от текущего наименования.
	| Если он был сгенерирован ранее, то изменять его рекомендуется только при уверенности корректного обращения к нему.
	| Контроль уникальности в пределах базы определяется во время записи. ПРОДОЛЖИТЬ?";
	ОписаниеОповещения = Новый ОписаниеОповещения("ИдентификаторПроцессаНачалоВыбораЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроцессаНачалоВыбораЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЛатиницей = EUF_ПроводникШлюзБСПКлиентСервер.СтрокаЛатиницей(Объект.Наименование);	
	СтрокаЛатиницей = СокрЛП(СтрокаЛатиницей);
	СтрокаЛатиницей = СтрЗаменить(СтрокаЛатиницей, " ", "_");
	Объект.ИдентификаторПроцесса = НРег(СтрокаЛатиницей); 
	
КонецПроцедуры

#КонецОбласти  

#Область ОбработчикиКомандФормы

 &НаКлиенте
Процедура ОткрытьФормуНастроекПараметров(Команда)
	
	ОбработкаНастроекСсылка = Объект.ИспользуемаяВнешняяОбработка;
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ДополнительнаяОбработкаСсылка", ОбработкаНастроекСсылка);
		
	ИмяОбработки = ПолучитьИмяОбработки(ОбработкаНастроекСсылка);
	Попытка	
		ОткрытьФорму("ВнешняяОбработка." + ИмяОбработки + ".Форма.ФормаУстановкиПараметров",
							ПараметрыОткрытияФормы, ЭтотОбъект, , , , ,
							РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Исключение
		ТекстОшибки = "Не удалось открыть форму настроек " + ИмяОбработки
					+ ". В обработке должна присутствовать форма ""ФормаУстановкиПараметров""";
		EUF_ПроводникШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяОбработки(ОбработкаНастроекСсылка)
	
	ИмяОбработки = EUF_ПроводникШлюзБСПСервер.ПодключитьВнешнююОбработку(ОбработкаНастроекСсылка);
	Возврат ИмяОбработки;
	
КонецФункции	

#КонецОбласти