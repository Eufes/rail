
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним реквизиты формы из параметров.
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,
		"АлгоритмЗаполнения, НеВыгружать, ЭлементТаблицы, Комментарий");
		
	Заголовок = Параметры.ИмяПоля + " ()";	
			
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВТаблицу = Истина;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВТаблицу  = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	ИначеЕсли Модифицированность И НЕ ПеренестиВТаблицу Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
	Если Отказ Тогда
		ПеренестиВТаблицу = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ПеренестиВТаблицу Тогда
		
		СтруктураРезультат = Новый Структура();
		
		СтруктураРезультат.Вставить("ФормулаТега", 		СокрЛП(АлгоритмЗаполнения));
		СтруктураРезультат.Вставить("Комментарий", 		Комментарий);
		СтруктураРезультат.Вставить("НеВыгружать", 		НеВыгружать);
		СтруктураРезультат.Вставить("ЭлементТаблицы", 	ЭлементТаблицы);
		 
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтаФорма);
		ОповеститьОВыборе(СтруктураРезультат);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ФормаРедактированияСтроки_Закрыть" И Источник = ВладелецФормы Тогда
		// Сообщение от основной формы документа при нажатии там Esc.
		// Сбрасываем флаг модифицированности и закрываем форму редактирования строки без вопросов.
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторФорматнойСтроки(Команда)
	
	Конструктор = Новый КонструкторФорматнойСтроки();
	
	Конструктор.Показать(Новый ОписаниеОповещения("ОткрытьКонструкторФорматнойСтрокиЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторФорматнойСтрокиЗавершение(Текст, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда
		АлгоритмЗаполнения = АлгоритмЗаполнения + Символы.ПС + Текст;
	КонецЕсли;
	
КонецПроцедуры


