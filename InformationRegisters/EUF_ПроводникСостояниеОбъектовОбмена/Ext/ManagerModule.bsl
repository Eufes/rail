#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

Функция УстановитьСостояниеОбъектаОбмена(СтруктураЗаписи) Экспорт

	Результат = "";
	
	ПроверяемыеРеквизиты = Новый Массив;
	ПроверяемыеРеквизиты.Добавить("Объект");
	ПроверяемыеРеквизиты.Добавить("ВнешняяСистема");
	
	Для Каждого ПроверяемыйРеквизит Из ПроверяемыеРеквизиты Цикл
		Если СтруктураЗаписи.Свойство(ПроверяемыйРеквизит)
			И Не ЗначениеЗаполнено(СтруктураЗаписи[ПроверяемыйРеквизит]) Тогда
			
			ОписаниеСобытия = НСтр("ru = 'Добавление записи регистра сведений ""Состояние объектов обмена (QIWI адаптер)""'");
			Комментарий     = НСтр("ru = 'Не заполнен реквизит %1. Создание записи регистра невозможно.'");
			Комментарий     = СтрШаблон(Комментарий, ПроверяемыйРеквизит);
			ЗаписьЖурналаРегистрации(ОписаниеСобытия, 
			                         УровеньЖурналаРегистрации.Ошибка,
			                         Метаданные.РегистрыСведений.EUF_ПроводникСостояниеОбъектовОбмена,
			                         ,
			                         Комментарий);
			
			Возврат Комментарий;
			
		КонецЕсли;
	КонецЦикла;	
	
	// Устанавливаем состояние	
	МенеджерЗаписи = РегистрыСведений.EUF_ПроводникСостояниеОбъектовОбмена.СоздатьМенеджерЗаписи(); 
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураЗаписи);
	Если СтруктураЗаписи.Свойство("ДатаСостояния") Тогда
		МенеджерЗаписи.ДатаСостояния = СтруктураЗаписи.ДатаСостояния;
	Иначе
		МенеджерЗаписи.ДатаСостояния = ТекущаяДатаСеанса();
	КонецЕсли;	
	МенеджерЗаписи.Записать();
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли