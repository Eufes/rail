///////////////////////////////////////////////////////////////////////////////////////////////////////
// Software Authors Name = "Mikhaylov Roman Vadimovich"
// Copyright (c) 2022, Mikhaylov Roman Vadimovich
// Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0)
// https://creativecommons.org/licenses/by-nc-sa/4.0/
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВерсияПодсистемы = "1.0.1.21";
	ЭтотОбъект.Заголовок = "≡●_Версия " + ВерсияПодсистемы;
	
	СписокДинамическихСписков.Очистить();
	СписокДинамическихСписков.Добавить("ДС_НастройкиТиповСообщений");
	СписокДинамическихСписков.Добавить("ДС_СостояниеОбъектовОбмена");
	СписокДинамическихСписков.Добавить("ОбщийЛогАаптера");
	
	// Соответствие страниц динамическим спискам
	СоответствиеСтраницДС = Новый Соответствие;
	СоответствиеСтраницДС.Вставить("СтраницаНастройкиТиповСообщений", "ДС_НастройкиТиповСообщений");
	СоответствиеСтраницДС.Вставить("СтраницаРегистры", 				  "ДС_НастройкиТиповСообщений");
	
	СоответствиеСтраницДС.Вставить("СтраницаСостоянияОбъектовОбмена", "ДС_СостояниеОбъектовОбмена");	 
	СоответствиеСтраницДС.Вставить("СтраницаРучноеУправление", "ОбщийЛогАаптера");
	СтраницыДС = Новый ФиксированноеСоответствие(СоответствиеСтраницДС);
	
	
	Для Каждого ДС Из СписокДинамическихСписков Цикл
		
		// Для исключения автоматического выполнения запросов всех динамических списков
		// устанавливаем запрет через специальный параметр формирования 
		УстановленПараметрФормирования = Этаформа[ДС.Значение].Параметры.Элементы.Найти("Формировать");
		Если УстановленПараметрФормирования <> Неопределено Тогда
			УстановленПараметрФормирования.Использование = Истина;
			УстановленПараметрФормирования.Значение = Ложь;
		Иначе
			ТекстОшибки = "Проверьте наличие параметра ""формировать"" у динамического списка " + ДС.Значение;
			MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
		
	КонецЦикла; 	
	
	// Для карты
	СоответствиеОбъектовИФормВР = Новый Соответствие;
	СоответствиеОбъектовИФормВР.Вставить("ПараметрыПодключений",
											"Справочник.MultiPort_ПодключенияКВнешнимСистемам.ФормаСписка");
	СоответствиеОбъектовИФормВР.Вставить("ТипыИсходящихСообщений",
											"Справочник.MultiPort_ТипИсходящегоСообщения.ФормаСписка");
	СоответствиеОбъектовИФормВР.Вставить("ТипыВходящихСообщений",
											"Справочник.MultiPort_ТипВходящегоСообщения.ФормаСписка");
	СоответствиеОбъектовИФормВР.Вставить("ИнтеграционныеПроцессы",
											"Справочник.MultiPort_ИнтеграционныеПроцессы.ФормаСписка");
	СоответствиеОбъектовИФормВР.Вставить("ВнешниеСистемы",
											"ПланОбмена.MultiPort_ВнешниеСистемы.ФормаСписка");
	СоответствиеОбъектовИФормВР.Вставить("РегламентныеЗадания",
											"Обработка.РегламентныеИФоновыеЗадания.Форма");
	СоответствиеОбъектовИФормВР.Вставить("НастройкиДляТиповСообщений",
											"РегистрСведений.MultiPort_ИспользуемыеНастройкиДляСообщений.ФормаСписка");
	СоответствиеОбъектовИФормВР.Вставить("ПодпискиНаСобытия",
											"РегистрСведений.MultiPort_ПодпискиОбмена.ФормаСписка");
	СоответствиеОбъектовИФорм = Новый ФиксированноеСоответствие(СоответствиеОбъектовИФормВР);
	
	ОбновитьДоступностьАдаптераНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Для началььной страницы обновляем данные динамического списка
	ДинамическийСписокПриСменеСтраницы(Элементы.СтраницаРучноеУправление);
	
	ИспользованиеУказанныхНастроекИсходящих();
	ИспользованиеУказанныхНастроекВходящих();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранныеЗначения)
	
	Если ТипЗнч(ВыбранныеЗначения) = Тип("Структура") Тогда
		
		РезультатЗапроса = ПолучитьИзВременногоХранилища(ВыбранныеЗначения.ДанныеВыбора);
		
		Если ТипЗнч(РезультатЗапроса) = Тип("Массив") Тогда
			
			РезультатЗапроса = РезультатЗапроса[РезультатЗапроса.ВГраница()];
			
			Если РезультатЗапроса.Колонки.Найти("Ссылка") <> Неопределено Тогда
				ВыбранныеСсылки = РезультатЗапроса.Выгрузить();
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ВыбранныеСсылки = ВыбранныеЗначения;
		
	КонецЕсли;
	
	Для Каждого Значение Из ВыбранныеСсылки Цикл
		
		НоваяСтрока = СписокЗарегистрированныхИсходящихСообщений.Добавить();
		НоваяСтрока.ИсходящееСообщение = Значение.Ссылка;
				
	КонецЦикла
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьИсходящееСообщение(Команда)
	
	Если Не ЗначениеЗаполнено(ВыбраннаяВнешняяСистема) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не выбрана внешняя система",
				, "ВыбраннаяВнешняяСистема");		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыгружаемыйОбъектСсылка) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не указан выгружаемый объект",
				, "ВыгружаемыйОбъектСсылка");		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТипИсходящегоСообщения) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не указан тип сообщения",
				, "ТипИсходящегоСообщения");		
		Возврат;
	КонецЕсли;
			
	СоздатьИсходящееСообщениеНаСервере();
	ДинамическийСписокПриСменеСтраницы(Элементы.СтраницаРучноеУправление);
	ОбновитьОбщийЛог();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции   

#Область РаботаСИсходящими //*************************************************

&НаКлиенте
Процедура КодФормированияПроизвольныхДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	КодФормированияПроизвольныхДанных =
	"СтруктураДанных = Новый Структура(""СохранитьНаДиск"", Ложь);
	|ПроизвольныеДанные = СтруктураДанных;
	|";
	
КонецПроцедуры

&НаСервере
Процедура СоздатьИсходящееСообщениеНаСервере()
	
	ПараметрыСообщения = MultiPort_Обмен.НовыйПараметрыСозданияИсходящих(ТипИсходящегоСообщения);
	ПараметрыСообщения.Вставить("ВыгружаемыйОбъектСсылка", ВыгружаемыйОбъектСсылка);
	ПараметрыСообщения.Вставить("СсылкаНаСвязанноеСообщение", СсылкаНаСвязанноеСообщение);
	
	Если ЗначениеЗаполнено(КодФормированияПроизвольныхДанных) Тогда
		Попытка
			ПроизвольныеДанные = Неопределено;
			УстановитьБезопасныйРежим(Истина);
			Выполнить(КодФормированияПроизвольныхДанных);
			ПараметрыСообщения.Вставить("ПроизвольныеДанные", ПроизвольныеДанные); 
		Исключение
			ТекстОшибок = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибок, ,
						"КодФормированияПроизвольныхДанных");
		КонецПопытки;
	КонецЕсли;
	
	ИсходящееСообщение = MultiPort_Обмен.СоздатьИсходящееСообщение(ПараметрыСообщения, ТипИсходящегоСообщения);
	Если Не ЗначениеЗаполнено(ИсходящееСообщение) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не удалось создать сообщение. См. лог адаптера");
		Возврат;	
	КонецЕсли;
	
	ТекущееИсходящееСообщение = ИсходящееСообщение;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьВИсходящейОчереди(Команда)
	
	Если Не ЗначениеЗаполнено(ТекущееИсходящееСообщение) Тогда 
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не определено исходящее сообщение",
					, "ТекущееИсходящееСообщение");		
		Возврат;
	КонецЕсли; 	
		
	ЗарегистрироватьВИсходящейОчередиНаСервере();
	ДинамическийСписокПриСменеСтраницы(Элементы.СтраницаРучноеУправление);
	ОбновитьОбщийЛог();
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьВИсходящейОчередиНаСервере()
	
	ДанныеОбмена = MultiPort_Обмен.НовыйПараметрыСозданияИсходящих(ТипИсходящегоСообщения);	
	ДанныеОбмена.Вставить("ВнешняяСистема", ВыбраннаяВнешняяСистема);
	ДанныеОбмена.Вставить("ИсходящееСообщение", ТекущееИсходящееСообщение);
	ДанныеОбмена.Вставить("ВыгружаемыйОбъектСсылка", ВыгружаемыйОбъектСсылка);
	ДанныеОбмена.Вставить("РучнаяРегистрация", Истина);
	
	ДанныеОбмена.Вставить("ТекущийПользователь", MultiPort_ШлюзБСПСервер.ТекущийПользователь());
	MultiPort_Обмен.ЗарегистрироватьВИсходящуюОчередь(ДанныеОбмена, ТипИсходящегоСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИсходящееСообщение(Команда)
	
	Если ИспользоватьУказанныйТипИНастройки
		И Не ЗначениеЗаполнено(ТекущаяНастройкаДляИсходящего) Тогда
		Таймаут = 20;
		ПоказатьПредупреждение(, "Не заполнена настройка для исходящего", Таймаут);
		Возврат;
	КонецЕсли;
	ОбработатьИсходящееСообщениеНаСервере(ТекущееИсходящееСообщение, ТипИсходящегоСообщения);
	ДинамическийСписокПриСменеСтраницы(Элементы.СтраницаРучноеУправление);
	ОбновитьОбщийЛог();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИсходящееСообщениеНаСервере(ТекущееИсходящееСообщение, ТипИсходящегоСообщения)
	
	Если ИспользоватьУказанныйТипИНастройки Тогда
		
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&ВнешняяСистема КАК ВнешняяСистема,
		|	СообщенияИсходящие.Ссылка КАК ИсходящееСообщение,
		|	СообщенияИсходящие.Ссылка.УникальноеВремяСоздания КАК УникальноеВремяРегистрации,
		|	&КонкретныйТипСообщения КАК ТипИсходящегоСообщения,
		|	ВЫБОР
		|		КОГДА ОчередьИсходящих.ИсходящееСообщение ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьВОчереди
		|ПОМЕСТИТЬ ПулИсходящих
		|ИЗ
		|	Справочник.MultiPort_СообщенияИсходящие КАК СообщенияИсходящие
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.MultiPort_ОчередьИсходящих КАК ОчередьИсходящих
		|		ПО СообщенияИсходящие.Ссылка = ОчередьИсходящих.ИсходящееСообщение
		|ГДЕ
		|	СообщенияИсходящие.Ссылка = &ТекущееИсходящееСообщение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТипИсходящегоСообщения.Ссылка КАК ТипИсходящегоСообщения
		|ПОМЕСТИТЬ ТипыСообщений
		|ИЗ
		|	Справочник.MultiPort_ТипИсходящегоСообщения КАК ТипИсходящегоСообщения
		|ГДЕ
		|	ТипИсходящегоСообщения.Ссылка = &КонкретныйТипСообщения"
		
		// Разделитель
		+ "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|"
		+ MultiPort_Обмен.ТекстЗапросаИсходящихСНастройкамиОбработки();
		
		Запрос.УстановитьПараметр("РежимОтладки", Истина);
		Запрос.УстановитьПараметр("ВнешняяСистема", ВыбраннаяВнешняяСистема);
		Запрос.УстановитьПараметр("КонкретныйТипСообщения", ТипИсходящегоСообщения);
		Запрос.УстановитьПараметр("ТекущееИсходящееСообщение", ТекущееИсходящееСообщение);
		
		Если Не ЗначениеЗаполнено(ТекущаяНастройкаДляИсходящего) Тогда
			Запрос.УстановитьПараметр("КонкретнаяНастройка", ТекущаяНастройкаДляИсходящего);
		Иначе 
			Запрос.УстановитьПараметр("КонкретнаяНастройка", Неопределено);
		КонецЕсли;	
		
		Запрос.Текст = ТекстЗапроса; 
		
		Результат = MultiPort_Обмен.ОбработкаИсходящихСообщений(Запрос); 
		
	Иначе
		
		Результат = MultiPort_Обмен.ПодготовитьОдноИсходящееКВыгрузке(ТекущееИсходящееСообщение, Истина);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСообщениеВоВнешнююСистему(Команда)
	
	Если ИспользоватьУказанныйТипИНастройки Тогда
		Если Не ЗначениеЗаполнено(ВыбраннаяВнешняяСистема) Тогда
			ТекстОшибки = "Не выбрана внешняя система";
			MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(ТекущееИсходящееСообщение) Тогда
			ТекстОшибки = "Не выбрано сообщение для выгрузки";
			MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);		
			Возврат;
		КонецЕсли;    
	КонецЕсли;
	
	ВыгрузитьСообщениеВоВнешнююСистемуНаСервере();
	ОбновитьОбщийЛог();
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьСообщениеВоВнешнююСистемуНаСервере()
	
	Если ИспользоватьУказанныйТипИНастройки Тогда
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВнешниеСистемы.Ссылка КАК ВнешняяСистема,
		|	ВнешниеСистемы.ПараметрыПодключения КАК ПараметрыПодключенияСсылка,
		|	СообщенияИсходящие.Ссылка КАК ИсходящееСообщение,
		|	СообщенияИсходящие.Ссылка.УникальноеВремяСоздания КАК УникальноеВремяРегистрации,
		|	&КонкретныйТипСообщения КАК ТипИсходящегоСообщения,
		|	ВЫБОР
		|		КОГДА ОчередьИсходящих.ИсходящееСообщение ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьВОчереди
		|ПОМЕСТИТЬ ПулИсходящих
		|ИЗ
		|	Справочник.MultiPort_СообщенияИсходящие КАК СообщенияИсходящие
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.MultiPort_ВнешниеСистемы КАК ВнешниеСистемы
		|		ПО (ВнешниеСистемы.Ссылка = &ВнешняяСистема)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.MultiPort_ОчередьИсходящих КАК ОчередьИсходящих
		|		ПО СообщенияИсходящие.Ссылка = ОчередьИсходящих.ИсходящееСообщение
		|ГДЕ
		|	СообщенияИсходящие.Ссылка = &ТекущееИсходящееСообщение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТипИсходящегоСообщения.Ссылка КАК ТипИсходящегоСообщения
		|ПОМЕСТИТЬ ТипыСообщений
		|ИЗ
		|	Справочник.MultiPort_ТипИсходящегоСообщения КАК ТипИсходящегоСообщения
		|ГДЕ
		|	ТипИсходящегоСообщения.Ссылка = &КонкретныйТипСообщения"
		
		// Разделитель
		+ "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|"
		+ MultiPort_Обмен.ТекстЗапросаИсходящихСНастройкамиВыгрузки();
		
		Запрос.УстановитьПараметр("РежимОтладки", Истина);
		Запрос.УстановитьПараметр("ВнешняяСистема", ВыбраннаяВнешняяСистема);
		Запрос.УстановитьПараметр("ТекущийУзел",  ПланыОбмена.MultiPort_ВнешниеСистемы.ЭтотУзел());
		Запрос.УстановитьПараметр("КонкретныйТипСообщения", ТипИсходящегоСообщения);
		Запрос.УстановитьПараметр("ТекущееИсходящееСообщение", ТекущееИсходящееСообщение);
		Если Не ЗначениеЗаполнено(ТекущаяНастройкаДляИсходящего) Тогда
			Запрос.УстановитьПараметр("КонкретнаяНастройка", ТекущаяНастройкаДляИсходящего);
		Иначе 
			Запрос.УстановитьПараметр("КонкретнаяНастройка", Неопределено);
		КонецЕсли;	
		
		Запрос.Текст = ТекстЗапроса; 
		
		Результат = MultiPort_Обмен.ВыгрузитьСообщенияВоВнешнююСистему(Запрос, ВыбраннаяВнешняяСистема);
	Иначе
		Результат = MultiPort_Обмен.ОтправитьОдноСообщение(ТекущееИсходящееСообщение, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОбъектВФайл(Команда)
	
	Если ПустаяСтрока(ПутьДляВыгрузкиВФайл) Тогда
		ТекстОшибки = "Укажите путь к файлу";
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущееИсходящееСообщение) Тогда
		ТекстОшибки = "Не указано исходящее сообщение";
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);		
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется выгрузка данных. Пожалуйста, подождите...'"));
	СтруктураДанных = Новый Структура("ВыгружаемыйОбъектСсылка", ВыгружаемыйОбъектСсылка);
	
	АдресФайлаДанныхВХранилище = ВыгрузитьДанныеВФайлНаСервере(СтруктураДанных, ТекущееИсходящееСообщение);
	
	Если АдресФайлаДанныхВХранилище = Неопределено
		ИЛИ АдресФайлаДанныхВХранилище = "" Тогда
		ТекстОшибки = "Не удалось выполнить выгрузку в файл";
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ПараметрыДиалогаПолученияФайлов = Новый ПараметрыДиалогаПолученияФайлов(Заголовок, Ложь);
	НачатьПолучениеФайлаССервера(АдресФайлаДанныхВХранилище, ПутьДляВыгрузкиВФайл, ПараметрыДиалогаПолученияФайлов);
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьДанныеВФайлНаСервере(СтруктураДанных, ТекущееИсходящееСообщение)
		
	// Если выгрузка по КД
	// ... 
	
	// Если выгрузка по мэпингу ()
	
	// Если выгрузка программным кодом
		
	ПутьВременногоФайлаВыгрузки = ПолучитьИмяВременногоФайла("txt");
	
	ДанныеИзХранилища = MultiPort_Обмен.ПолучитьДанныеФорматаТранспорта(ТекущееИсходящееСообщение);
	ДанныеВФорматеТранспорта = ДанныеИзХранилища.Получить();
	
	Если ДанныеВФорматеТранспорта = Неопределено Тогда
		ТекстОшибки = "Данные транспорта пустые.";
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);		
		Возврат Неопределено; 	
	ИначеЕсли ТипЗнч(ДанныеВФорматеТранспорта) = Тип("Строка") Тогда
		ТекстДок = Новый ТекстовыйДокумент;
		ТекстДок.УстановитьТекст(ДанныеВФорматеТранспорта);
		ТекстДок.Записать(ПутьВременногоФайлаВыгрузки, "UTF-8");
	Иначе     
		ТекстОшибки = "Данные транспорта невозможно сохранить в файл";
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);		
		Возврат Неопределено;
	КонецЕсли;	
	
	АдресФайлаДанных = ПоместитьВыгрузкуВоВременноеХранилище(ПутьВременногоФайлаВыгрузки);
	
	Возврат АдресФайлаДанных;
	
КонецФункции

&НаСервере
Функция ПоместитьВыгрузкуВоВременноеХранилище(ИмяФайлаОбменаНаСервере)
	
	ФайлДД = Новый ДвоичныеДанные(ИмяФайлаОбменаНаСервере);
	
	АдресФайлаДанных = ПоместитьВоВременноеХранилище(ФайлДД, УникальныйИдентификатор);
	УдалитьФайлы(ИмяФайлаОбменаНаСервере);

	Возврат АдресФайлаДанных;
	
КонецФункции

#КонецОбласти // РаботаСИсходящими

#Область РаботаСВходящими //********************************************

&НаКлиенте
Процедура СоздатьВходящееСообщение(Команда)

	Если Не ЗначениеЗаполнено(ВыбраннаяВнешняяСистема) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не выбрана внешняя система",
				, "ВыбраннаяВнешняяСистема");		
		Возврат;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ТипВходящегоСообщения) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не указан тип сообщения",
				, "ТипВходящегоСообщения");		
		Возврат;
	КонецЕсли;
		
	СоздатьВходящееСообщениеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВходящееСообщениеНаСервере()
	
	// Получение настроек типа сообщения	
	НастройкиВходящегоОбращения = MultiPort_Обмен.ПолучитьНастройкиВходящего(ТипВходящегоСообщения);	
	Если Не ЗначениеЗаполнено(НастройкиВходящегоОбращения.НастройкаСсылка) Тогда
		ИнфоДляВнешнейСистемы = " Не удалось определить настройки для входящего сообщения во внешней системе.";
		MultiPort_ВызовСервера.ВыполнитьЛогирование("Входящий запрос", УровеньЖурналаРегистрации.Ошибка,
														ИнфоДляВнешнейСистемы, , Истина);
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ИнфоДляВнешнейСистемы);												
	КонецЕсли;
	
	ИдентификаторСообщенияВС = ?(Не ЗначениеЗаполнено(ИдентификаторСообщенияВС),
									Новый УникальныйИдентификатор, ИдентификаторСообщенияВС); 
									
	НастройкиВходящегоОбращения.Вставить("ИдентификаторСообщенияВС", ИдентификаторСообщенияВС);
			
	// Создание входящего сообщения
	ПараметрыОбмена	= Новый Структура();
	ПараметрыОбмена.Вставить("ТипВходящегоСообщения", 	 ТипВходящегоСообщения);
	ПараметрыОбмена.Вставить("ВходящиеДанные", 			 ВходящееСообщениеСтрокой);
	ПараметрыОбмена.Вставить("ТипСодержимого", 			 ТипСодержимого);
	
	Попытка
		ТекущееВходящееСообщение = MultiPort_Обмен.СоздатьВходящееСообщение(НастройкиВходящегоОбращения,
								ПараметрыОбмена);			
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаголовокОшибки = "Ошибка создания сообщения входящего";
		MultiPort_ВызовСервера.ВыполнитьЛогирование(ЗаголовокОшибки, УровеньЖурналаРегистрации.Ошибка, ТекстОшибки,
									ТипВходящегоСообщения, Истина);
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ТекстОшибки);	
		
	КонецПопытки;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьВоВходящейОчереди(Команда)
	
	Если Не ЗначениеЗаполнено(ВыбраннаяВнешняяСистема) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не выбрана внешняя система",
				, "ВыбраннаяВнешняяСистема");		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущееВходящееСообщение) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не выбрано входящее сообщение",
				, "ТекущееВходящееСообщение");		
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьВоВходящейОчередиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьВоВходящейОчередиНаСервере()
	
	// Получение настроек типа сообщения	
	НастройкиВходящегоОбращения = MultiPort_Обмен.ПолучитьНастройкиВходящего(ТипВходящегоСообщения);	
	Если Не ЗначениеЗаполнено(НастройкиВходящегоОбращения.НастройкаСсылка) Тогда
		ИнфоДляВнешнейСистемы = " Не удалось определить настройки для входящего сообщения во внешней системе.";
		MultiPort_ВызовСервера.ВыполнитьЛогирование("Входящий запрос", УровеньЖурналаРегистрации.Ошибка,
														ИнфоДляВнешнейСистемы, , Истина);
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю(ИнфоДляВнешнейСистемы);
	КонецЕсли;
	
	ИдентификаторСообщенияВС = ?(Не ЗначениеЗаполнено(ИдентификаторСообщенияВС),
									Новый УникальныйИдентификатор, ИдентификаторСообщенияВС); 
	
	ПараметрыОбмена = Новый Структура;
	// Фиксируем уникальное время для очереди до выполнения последующих процедур и функций
	ПараметрыОбмена.Вставить("УникальноеВремяРегистрации", ТекущаяУниверсальнаяДатаВМиллисекундах());	
	ПараметрыОбмена.Вставить("ВнешняяСистема", 			 ВыбраннаяВнешняяСистема);
	ПараметрыОбмена.Вставить("ВходящееСообщение", 		 ТекущееВходящееСообщение);
	ПараметрыОбмена.Вставить("ТипВходящегоСообщения", 	 ТипВходящегоСообщения);
	ПараметрыОбмена.Вставить("ВходящиеДанные", 			 ВходящееСообщениеСтрокой);
	ПараметрыОбмена.Вставить("ТипСодержимого", 			 ТипСодержимого);
	ПараметрыОбмена.Вставить("ИдентификаторСообщенияВС", ИдентификаторСообщенияВС);
	ПараметрыОбмена.Вставить("ИдентификаторПроцесса", 	 ИдентификаторПроцесса);
		
	MultiPort_Обмен.ЗарегистрироватьВоВходящуюОчередь(ПараметрыОбмена, НастройкиВходящегоОбращения);	
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьВходящееСообщение(Команда)
	
	Если Не ЗначениеЗаполнено(ТекущееВходящееСообщение) Тогда
		MultiPort_ШлюзБСПКлиентСервер.СообщитьПользователю("Не выбрано входящее сообщение",
				, "ТекущееВходящееСообщение");		
		Возврат;
	КонецЕсли;
	
	ОбработатьВходящееСообщениеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВходящееСообщениеНаСервере()	
	
	Если ИспользоватьУказанныйТипИНастройкиВходящего Тогда
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&ВнешняяСистема КАК ВнешняяСистема,
		|	СообщенияВходящие.Ссылка КАК ВходящееСообщение,
		|	СообщенияВходящие.Ссылка.УникальноеВремяСоздания КАК УникальноеВремяРегистрации,
		|	&КонкретныйТипСообщения КАК ТипВходящегоСообщения
		|ПОМЕСТИТЬ ПулВходящих
		|ИЗ
		|	Справочник.MultiPort_СообщенияВходящие КАК СообщенияВходящие
		|ГДЕ
		|	СообщенияВходящие.Ссылка = &ТекущееВходящееСообщение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТипВходящегоСообщения.Ссылка КАК ТипВходящегоСообщения
		|ПОМЕСТИТЬ ТипыСообщений
		|ИЗ
		|	Справочник.MultiPort_ТипВходящегоСообщения КАК ТипВходящегоСообщения
		|ГДЕ
		|	ТипВходящегоСообщения.Ссылка = &КонкретныйТипСообщения"
		
		// Разделитель
		+ "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|"
		+ MultiPort_Обмен.ТекстЗапросаВходящихСНастройкамиОбработки() 
		
		// Разделитель
		+ "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|"
		+ 
		
		"ВЫБРАТЬ
		|	ПулВходящих.ВходящееСообщение КАК ВходящееСообщение,
		|	НастройкиПоТипамСообщений.НастройкаСсылка КАК НастройкаСсылка,
		|	ЕСТЬNULL(ДанныеВФорматеТранспорта.ДанныеОбмена, НЕОПРЕДЕЛЕНО) КАК ДанныеВФорматеТранспорта,
		|	ЕСТЬNULL(ДанныеВФорматеТранспорта.ОписаниеФормата, """") КАК ОписаниеФормата,
		|	ВЫРАЗИТЬ(ПулВходящих.ВходящееСообщение КАК Справочник.MultiPort_СообщенияВходящие).СчетчикПопытокОбработки КАК ИзрасходованоПопыток
		|ИЗ
		|	ПулВходящих КАК ПулВходящих
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.MultiPort_ДанныеВФорматеТранспорта КАК ДанныеВФорматеТранспорта
		|		ПО ПулВходящих.ВходящееСообщение = ДанныеВФорматеТранспорта.Сообщение
		|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиПоТипамСообщений КАК НастройкиПоТипамСообщений
		|		ПО ПулВходящих.ТипВходящегоСообщения = НастройкиПоТипамСообщений.ТипВходящегоСообщения";	
		
		Запрос.УстановитьПараметр("РежимОтладки", Истина);
		Запрос.УстановитьПараметр("ВнешняяСистема", ВыбраннаяВнешняяСистема);
		Запрос.УстановитьПараметр("ТекущееВходящееСообщение", ТекущееВходящееСообщение);
		Запрос.УстановитьПараметр("КонкретныйТипСообщения", ТипВходящегоСообщения);
		
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса); 
		
		// ──────────────────────────────
		// Определяем поля настроек
		ОператорВыбора = СхемаЗапроса.ПакетЗапросов[4].Операторы[0];	
		MultiPort_Обмен.ДобавитьПоляВыборкиНастроекВходящих(ОператорВыбора);
		
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();	
		Запрос.Текст = ТекстЗапроса; 
		
		MultiPort_Обмен.ОбработкаВходящихСообщений(Запрос);
		
	Иначе
		
		Результат = MultiPort_Обмен.ОбработатьОдноВходящееСообщение(ТекущееВходящееСообщение, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // РаботаСВходящими

#Область ИнтерфейсНавигация //************************************************

&НаСервере
Процедура ОтчетПоИнтеграционнымВзаимодействиямНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СхемаКомпоновкиДанных 	= ОбработкаОбъект.ПолучитьМакет("ОтчетПоИнтеграционнымВзаимодействиям");
	НастройкиКомпоновки 	= СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	ДанныеРасшифровки 		= Новый ДанныеРасшифровкиКомпоновкиДанных;

	
	ПараметрыДанныхКомпоновки = НастройкиКомпоновки.ПараметрыДанных;
	ВнешниеСистемы = Новый Массив;
	ВнешниеСистемы.Добавить(ПланыОбмена.MultiPort_ВнешниеСистемы.ПустаяСсылка());
	ВнешниеСистемы.Добавить(ВыбраннаяВнешняяСистема);
	ПараметрыДанныхКомпоновки.УстановитьЗначениеПараметра("ВнешняяСистема", ВнешниеСистемы);
	
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки, ДанныеРасшифровки, , );
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ОтчетИнтеграционныхПроцессов);
	
	ОтчетИнтеграционныхПроцессов.Очистить();	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоИнтеграционнымВзаимодействиям(Команда)
	ОтчетПоИнтеграционнымВзаимодействиямНаСервере();
КонецПроцедуры 

&НаКлиенте
Процедура ИнтерфейсКартаВыбор(Элемент)
	
	ТекущийКубик = Элементы.КартаОбъектов.ТекущийЭлемент;
	
	Если ТекущийКубик = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущийКубик.Имя = "ПараметрыПодключений" Тогда
		ОткрытьФорму("Справочник.MultiPort_ПодключенияКВнешнимСистемам.ФормаСписка");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КартаНастройкиВыбор(Элемент)
	
    ТекущийКубик = Элементы.КартаНастройки.ТекущийЭлемент;
	
	Если ТекущийКубик = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФорме = СоответствиеОбъектовИФорм.Получить(ТекущийКубик.Имя);
	
	Если ПутьКФорме <> Неопределено Тогда
		ОткрытьФорму(ПутьКФорме);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУказанныйТипИНастройкиПриИзменении(Элемент)
	
	ИспользованиеУказанныхНастроекИсходящих();
	
КонецПроцедуры 

&НаКлиенте
Процедура ИспользованиеУказанныхНастроекИсходящих()
	
	Если ИспользоватьУказанныйТипИНастройки Тогда
		ЦветГруппы = MultiPort_ШлюзБСПКлиентСервер.ЦветСтиля("ЦветИзмененногоПоля");		
	Иначе
		ЦветГруппы = Новый Цвет();
	КонецЕсли;
	Элементы.ГруппаТипИсходящегоСообщения.ЦветФона = ЦветГруппы;
	Элементы.ГруппаРегистрацииИсходящего.Видимость = ИспользоватьУказанныйТипИНастройки;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУказанныйТипИНастройкиВходящегоПриИзменении(Элемент)
	
	ИспользованиеУказанныхНастроекВходящих();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеУказанныхНастроекВходящих()
	
	Элементы.ГруппаРегистрацияВходящего.Видимость = ИспользоватьУказанныйТипИНастройкиВходящего;
	
КонецПроцедуры

#КонецОбласти  // ИнтерфейсНавигация

#Область Вспомогательное //****************************************

&НаКлиенте
Процедура ОбновитьОбщийЛог()
	
	Элементы.ОбщийЛогАаптера.Обновить(); 
	КлючЗаписиРегистра = ПолучитьПоследнийКлючЗаписиПротокола();
	Элементы.ОбщийЛогАаптера.ТекущаяСтрока = КлючЗаписиРегистра;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнийКлючЗаписиПротокола()
	
	КлючЗаписиРегистра = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПротоколОбмена.Сообщение КАК Сообщение,
	|	ПротоколОбмена.УникальноеВремяСобытия КАК УникальноеВремяСобытия
	|ИЗ
	|	РегистрСведений.MultiPort_ПротоколОбмена КАК ПротоколОбмена
	|
	|УПОРЯДОЧИТЬ ПО
	|	УникальноеВремяСобытия УБЫВ";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
			
		КлючЗаписи = Новый Структура;
		КлючЗаписи.Вставить("Сообщение", Выборка.Сообщение);
		КлючЗаписи.Вставить("УникальноеВремяСобытия", Выборка.УникальноеВремяСобытия);
			
		МассивКлюча = Новый Массив;
		МассивКлюча.Добавить(КлючЗаписи);		
		КлючЗаписиРегистра = Новый("РегистрСведенийКлючЗаписи.MultiPort_ПротоколОбмена", МассивКлюча);
        
	КонецЕсли;
	
	Возврат КлючЗаписиРегистра;
	
КонецФункции	

&НаКлиенте
Процедура ПутьДляВыгрузкиВФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, ПутьДляВыгрузкиВФайл, Ложь, , , Ложь);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайла(Элемент, ИмяФайлаОбмена, ПроверятьСуществование, Знач РасширениеФайла = "json",	
						АрхивироватьФайлДанных = Ложь, РежимОбмена)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		
	Если АрхивироватьФайлДанных Тогда
		
		ДиалогВыбораФайла.Фильтр = "Архивный файл данных (*.zip)|*.zip";
		ДиалогВыбораФайла.Расширение = "zip";		
		
	ИначеЕсли РасширениеФайла = "txt" Тогда
		
		ДиалогВыбораФайла.Фильтр = "Файл протокола обмена (*.txt)|*.txt";
		ДиалогВыбораФайла.Расширение = "txt";
		
	ИначеЕсли РасширениеФайла = "json" Тогда
		
		ДиалогВыбораФайла.Фильтр = "Файл данных (*.json)|*.json";
		ДиалогВыбораФайла.Расширение = "json";
		
	ИначеЕсли РасширениеФайла = "xml" Тогда
		
		ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
		ДиалогВыбораФайла.Расширение = "xml";
		
	Иначе
		
		ДиалогВыбораФайла.Фильтр = "Файл данных (*.*)|*.*";
		
	КонецЕсли;
	
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = Элемент.ТекстРедактирования;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = ПроверятьСуществование;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда 		
		ИмяФайлаОбмена = ДиалогВыбораФайла.ПолноеИмяФайла;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИсходящегоСообщенияПриИзменении(Элемент)
	ТипИсходящегоСообщенияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТипИсходящегоСообщенияПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиСрезПоследних.НастройкаДляСообщения КАК НастройкаСсылка
	|ПОМЕСТИТЬ ВТ_Настройка
	|ИЗ
	|	РегистрСведений.MultiPort_ИспользуемыеНастройкиДляСообщений.СрезПоследних(, ТипСообщения = &ТипСообщения) КАК НастройкиСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройка.НастройкаСсылка КАК НастройкаСсылка
	|ИЗ
	|	ВТ_Настройка КАК ВТ_Настройка";
	Запрос.УстановитьПараметр("ТипСообщения", ТипИсходящегоСообщения);		
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекущаяНастройкаДляИсходящего = Выборка.НастройкаСсылка;		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СтраницыАдминистрированияПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ДинамическийСписокПриСменеСтраницы(ТекущаяСтраница);	
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыРегистровПриСменеСтраницы(Элемент, ТекущаяСтраница)
		
	ДинамическийСписокПриСменеСтраницы(ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ДинамическийСписокПриСменеСтраницы(ТекущаяСтраница)
		
	ИмяДинамическогоСписка = СтраницыДС.Получить(ТекущаяСтраница.Имя);
	
	Если ИмяДинамическогоСписка <> Неопределено Тогда
		
		УстановленПараметрФормирования = Этаформа[ИмяДинамическогоСписка].Параметры.Элементы.Найти("Формировать");
		УстановленПараметрФормирования.Использование = Истина;
		УстановленПараметрФормирования.Значение = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСсылкаНаЗаписьОчередиНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	Отбор = Новый Структура;
	Отбор.Вставить("ИсходящееСообщение", ТекущееИсходящееСообщение);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("РегистрСведений.MultiPort_ОчередьИсходящих.ФормаСписка", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИсходящиеСообщения(Команда)
		
	ПараметрыКонсолиЗапросов = Новый Структура;	
	ПараметрыКонсолиЗапросов.Вставить("ВариантИспользованияКонсолиЗапросов", 0);
	ПараметрыКонсолиЗапросов.Вставить("ПутьКВнешнейКонсолиЗапросов", "");
	
	ОткрытьФорму("Обработка.MultiPort_Администрирование.Форма.ВыборИзЗапроса", ПараметрыКонсолиЗапросов, ЭтотОбъект);
	   
КонецПроцедуры

&НаКлиенте
Процедура ВыгружаемыйОбъектСсылкаПриИзменении(Элемент)		
	GUIDОбъекта = ВыгружаемыйОбъектСсылка.УникальныйИдентификатор();	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийЛогАаптераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле.Имя = "ОбщийЛогАаптераСообщение" Тогда		
		СтандартнаяОбработка = Ложь;
		ТекДанные = Элемент.ТекущиеДанные;
		Если ТекДанные <> Неопределено Тогда
			Если ТипЗнч(ТекДанные.Сообщение) = Тип("СправочникСсылка.MultiPort_СообщенияИсходящие") Тогда
				ПараметрыОткрытия = Новый Структура("Ключ", ТекДанные.Сообщение);
				ОткрытьФорму("Справочник.MultiPort_СообщенияИсходящие.Форма.ФормаЭлемента", ПараметрыОткрытия);
			ИначеЕсли ТипЗнч(ТекДанные.Сообщение) = Тип("СправочникСсылка.MultiPort_СообщенияВходящие") Тогда
				ПараметрыОткрытия = Новый Структура("Ключ", ТекДанные.Сообщение);
				ОткрытьФорму("Справочник.MultiPort_СообщенияВходящие.Форма.ФормаЭлемента", ПараметрыОткрытия);
			Иначе	
				Возврат;
			КонецЕсли;		
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ОбщийЛогАаптераНаправление" Тогда
		СтандартнаяОбработка = Ложь;
		ТекДанные = Элемент.ТекущиеДанные;
		Если ТекДанные <> Неопределено Тогда
			Если ТипЗнч(ТекДанные.Сообщение) = Тип("СправочникСсылка.MultiPort_СообщенияИсходящие") Тогда
				ТекущееИсходящееСообщение = ТекДанные.Сообщение;
			ИначеЕсли ТипЗнч(ТекДанные.Сообщение) = Тип("СправочникСсылка.MultiPort_СообщенияВходящие") Тогда
				ТекущееВходящееСообщение = ТекДанные.Сообщение;
			Иначе	
				Возврат;
			КонецЕсли;		
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонсольРегламентныхЗаданий(Команда)
	
	ПутьККонсолиБСП = "Обработка.РегламентныеИФоновыеЗадания.Форма"; 
	ОткрытьФорму(ПутьККонсолиБСП,
	             ,
	             ЭтотОбъект,
	             Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВходящееСообщениеСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВходящееСообщениеСтрокойЗавершениеВвода", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеОЗакрытии, ВходящееСообщениеСтрокой, "Данные входящего сообщения", , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящееСообщениеСтрокойЗавершениеВвода(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
	
	Если ВведенныйТекст = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВходящееСообщениеСтрокой = ВведенныйТекст;
		
КонецПроцедуры

#КонецОбласти // Вспомогательное

&НаКлиенте
Процедура ДоступностьАдаптера(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьДоступностьАдаптера", ЭтотОбъект);
	ОткрытьФорму("Обработка.MultiPort_Администрирование.Форма.ФормаДоступностиРеглЗаданий",
						, ЭтаФорма, , , ,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьДоступностьАдаптера(Результат, ДопПараметры) Экспорт	
	ОбновитьДоступностьАдаптераНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьАдаптераНаСервере()
		
	Кнопка = Элементы.ДоступностьАдаптера;
	АдаптерДоступен = MultiPort_Обмен.РазрешенаРаботаПодсистемыОбмена();
	ЗаголовокКнопки = ?(АдаптерДоступен, "Адаптер включен", "Адаптер заблокирован");
	Картинка = ?(АдаптерДоступен, БиблиотекаКартинок.MultiPort_КругЗеленый, БиблиотекаКартинок.MultiPort_КругКрасный);
	Кнопка.Заголовок = ЗаголовокКнопки;
	Кнопка.Пометка = АдаптерДоступен; 
	Кнопка.Картинка = Картинка;
	
КонецПроцедуры	

#КонецОбласти // СлужебныеПроцедурыИФункции
